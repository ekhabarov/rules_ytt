"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", _http_archive = "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//ytt/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//ytt/private:versions.bzl", "YTT_VERSIONS")

def http_archive(name, **kwargs):
    maybe(_http_archive, name = name, **kwargs)

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
# This is all fixed by bzlmod, so we just tolerate it for now.
def rules_ytt_dependencies():
    "rules_ytt runtime dependencies."
    pass

def rules_ytt_internal_dependencies():
    "rules_ytt dependencies for internal use only."
    http_archive(
        name = "io_bazel_stardoc",
        sha256 = "ec57139e466faae563f2fc39609da4948a479bb51b6d67aedd7d9b1b8059c433",
        urls = [
            "https://mirror.bazel.build/github.com/bazelbuild/stardoc/releases/download/0.5.4/stardoc-0.5.4.tar.gz",
            "https://github.com/bazelbuild/stardoc/releases/download/0.5.4/stardoc-0.5.4.tar.gz",
        ],
    )
    http_archive(
        name = "aspect_bazel_lib",
        sha256 = "c858cc637db5370f6fd752478d1153955b4b4cbec7ffe95eb4a47a48499a79c3",
        strip_prefix = "bazel-lib-2.0.3",
        url = "https://github.com/aspect-build/bazel-lib/releases/download/v2.0.3/bazel-lib-v2.0.3.tar.gz",
    )

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for ytt toolchain"
_ATTRS = {
    "ytt_version": attr.string(mandatory = True, values = YTT_VERSIONS.keys()),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

_YTT_BUILD_TPL = """\
#Generated by ytt/repositories.bzl
load("@rules_ytt//ytt:toolchains.bzl", "ytt_toolchain")

ytt_toolchain(
    name = "ytt_toolchain",
    ytt = select({
        "@bazel_tools//src/conditions:host_windows": "ytt.exe",
        "//conditions:default": "ytt",
    }),
    version = "%s",
)
"""

def _ytt_repo_impl(rctx):
    url = "https://github.com/carvel-dev/ytt/releases/download/v{version}/ytt-{platform}".format(
        version = rctx.attr.ytt_version,
        platform = rctx.attr.platform.replace("_", "-"),
    )

    rctx.download(
        url = url,
        output = "ytt",
        integrity = YTT_VERSIONS[rctx.attr.ytt_version][rctx.attr.platform],
        executable = True,
    )

    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", _YTT_BUILD_TPL % rctx.attr.ytt_version)

ytt_repositories = repository_rule(
    _ytt_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def rules_ytt_register_toolchains(name = "ytt", version = "0.46.2", **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like `ytt_linux_amd64` -
      this repository is lazily fetched when node is needed for that platform.
    - create a repository exposing toolchains for each platform like `ytt_platforms`
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.

    Args:
        name: base name for all created repos, like "ytt"
        version: Ytt tool version. Supported versions are listed in
          [versions.bzl](/ytt/private/versions.bzl).
        **kwargs: passed to each node_repositories call
    """
    for platform in PLATFORMS.keys():
        ytt_repositories(
            name = name + "_" + platform,
            platform = platform,
            ytt_version = version,
            **kwargs
        )
        native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        ytt = name,
    )
